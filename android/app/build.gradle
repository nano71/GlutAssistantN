plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "org.jetbrains.kotlin.plugin.serialization" version "2.3.0"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader -> localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty("flutter.versionCode", "1")
def flutterVersionName = localProperties.getProperty("flutter.versionName", "1.0")

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.nano71.glutassistantn"
    ndkVersion "28.2.13676358"
    buildToolsVersion "35.0.0"
    compileSdkVersion 36
    flavorDimensions "version"

    defaultConfig {
        applicationId "com.nano71.glutassistantn"
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion 36
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    productFlavors {
        develop {
            applicationId "com.develop.glutassistantn"
            manifestPlaceholders = [TD_CHANNEL_ID: "develop"]
        }

        build {
            applicationId "com.nano71.glutassistantn"
            manifestPlaceholders = [TD_CHANNEL_ID: "build"]
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile keystoreProperties["storeFile"] ? file(keystoreProperties["storeFile"]) : null
            storePassword keystoreProperties["storePassword"]
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    lint {
        checkDependencies true
    }
}

flutter {
    source "../.."
}

dependencies {
    implementation "com.google.android.material:material:1.12.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.10.0"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.window:window:1.3.0"
    implementation "androidx.window:window-java:1.3.0"
    implementation "androidx.window:window-rxjava2:1.3.0"
    implementation "androidx.window:window-rxjava3:1.3.0"
    implementation "androidx.window:window-testing:1.3.0"
    implementation "androidx.core:core-splashscreen:1.2.0"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.4"
}

configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == "androidx.work") {
            details.useVersion "2.10.2"
        }
    }
}
